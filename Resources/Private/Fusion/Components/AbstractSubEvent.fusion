prototype(Neos.Neos:History.PublishedNode.AbstractSubEvent) >
prototype(Neos.Neos:History.PublishedNode.AbstractSubEvent) < prototype(Neos.Fusion:Component) {
    subEventType = ${Array.first(event).eventType}
    event = ${event}
    descriptionTranslationId = ''
    descriptionTranslationArguments = ${{}}
    linkedNode = Neos.Fusion:Template {
        templatePath = 'resource://Neos.Neos/Private/Templates/Module/Management/History/NodeLink.html'
        event = ${event}
        @process.trim = ${String.trim(value)}
    }
    user = Neos.Fusion:Template {
        templatePath = 'resource://Neos.Neos/Private/Templates/Module/Management/History/User.html'
        event = ${event}
        @process.trim = ${String.trim(value)}
    }

    @context.eventsOfMatchedType = ${[event]}

    @private {
        nodeTypeIcon = ${AE.History.Node.icon(props.event.node)}
        identifier = ${AE.History.Event.identifier(event)}
        userInitials = ${AE.History.Event.userName(event, 'initials')}
        changes = ${AE.History.Event.changes(props.event)}
        i18n = ${I18n.id('').source('Main').package('AE.History')}
        description = ${props.descriptionTranslationId ? private.i18n.id(props.descriptionTranslationId).source('Modules').package('Neos.Neos').arguments(props.descriptionTranslationArguments) : ''}
    }

    renderer = afx`
        <div class="neos-history-sub-event" id={private.identifier}>
            <div class="neos-history-event-user" title={private.userInitials}>
                {private.userInitials}
            </div>

            <div class="neos-history-event-description">
                {!props.event.data.old || !private.changes ? private.description : ''}
                <details
                    class="neos-history-event-details"
                    @if={props.event.data.old && private.changes}
                >
                    <summary>{private.description}</summary>
                    <div class="neos-content-diff">
                        <Neos.Fusion:Loop items={private.changes} itemName="change">
                            <div class="neos-content-diff__change">
                                <strong>{change.propertyLabel}</strong>
                                <AE.History:Component.Change.Text change={change} @if={change.type == 'text'}/>
                                <AE.History:Component.Change.Image change={change} @if={change.type == 'image'}/>
                                <AE.History:Component.Change.Asset change={change} @if={change.type == 'asset'}/>
                                <AE.History:Component.Change.DateTime change={change} @if={change.type == 'datetime'}/>
                            </div>
                        </Neos.Fusion:Loop>
                    </div>
                </details>
            </div>
        </div>
    `
}
